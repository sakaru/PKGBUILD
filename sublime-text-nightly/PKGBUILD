# Maintainer: Sander Boom <sander at inflowmotion dot nl>

pkgname=sublime-text-nightly
pkgver=3.3022
pkgrel=2
pkgdesc="sophisticated text editor for code, html and prose - nightly build (available to registered users only)"
arch=('i686' 'x86_64')
url="http://www.sublimetext.com/3dev"
license=('custom')
depends=(libpng gtk2)
makedepends=("unzip" "zip")
conflicts=(sublime-text sublime-text-dev)
provides=(sublime-text sublime-text-dev)
install=${pkgname}.install

_pkgrealname=sublime_text
_archurl='x64'

[[ "${CARCH}" = i686 ]] && _archurl='x32'

source=(
  "http://c758482.r82.cf2.rackcdn.com/sublime_text_${pkgver:0:1}_build_${pkgver:2}_${_archurl}.tar.bz2"
  "disable-update-notifications.patch"
)
md5sums=('34da7087a51135f50aca465aac3ae18b'
         '2f3ba1847ea83dceb126436846fb1c62')

[[ "${CARCH}" = i686 ]] && md5sums[0]='e12497a1902a76ea1b2e1bbe05a13617'

build() {
  # Patch the default Preferences package to disable update notifications
  cd "${srcdir}/sublime_text_3/Packages"
  mkdir tmp
  mv Default.sublime-package tmp/
  cd tmp
  unzip Default.sublime-package
  rm Default.sublime-package
  patch Preferences.sublime-settings < "${srcdir}/disable-update-notifications.patch"
  zip Default.sublime-package *
  mv Default.sublime-package ../
  cd ..
  rm -r tmp
}

package() {
  cd "${srcdir}"

  install -dm755 "${pkgdir}/opt"
  cp --preserve=mode -r "sublime_text_${pkgver:0:1}" "${pkgdir}/opt/${_pkgrealname}"

  for res in 128x128 16x16 256x256 32x32 48x48; do
    install -dm755 "${pkgdir}/usr/share/icons/hicolor/${res}/apps"
    ln -s "/opt/${_pkgrealname}/Icon/${res}/sublime-text.png" "${pkgdir}/usr/share/icons/hicolor/${res}/apps/sublime-text.png"
  done

  install -dm755 "${pkgdir}/usr/share/applications"
  ln -s "/opt/${_pkgrealname}/${_pkgrealname}.desktop" "${pkgdir}/usr/share/applications/${_pkgrealname}.desktop"

  install -dm755 "${pkgdir}/usr/bin"
  ln -s "/opt/${_pkgrealname}/${_pkgrealname}" "${pkgdir}/usr/bin/subl"
}
