# Maintainer: Sid Karunaratne <sid at karunaratne dot net>
pkgname=vowpal_wabbit
pkgver=7.2
pkgrel=1
pkgdesc="the essence of speed in machine learning, able to learn from terafeature datasets with ease"
arch=('i686' 'x86_64')
url="https://github.com/JohnLangford/vowpal_wabbit"
license=('BSD')
depends=(boost)
source=("https://github.com/JohnLangford/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=("0e04a731f8fba014abc3e913923f848644580612aff244f3df2ee63df40948b6")

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i configure.ac -e 's#AM_CONFIG_HEADER#AC_CONFIG_HEADERS#'
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make install DESTDIR="${pkgdir}"
}
